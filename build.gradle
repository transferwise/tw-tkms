import org.eclipse.jgit.api.errors.RefAlreadyExistsException

buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.14.12"
    }
}

plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.5.8' apply false
    id "com.github.spotbugs" version "5.0.3" apply false
    id "at.zierler.yamlvalidator" version "1.5.0"
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_11
    targetBytecodeVersion = JavaVersion.VERSION_11
}

yamlValidator {
    searchPaths = ['.circleci/']
    allowDuplicates = false
    searchRecursive = true
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.kafka"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}
