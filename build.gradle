import org.eclipse.jgit.api.errors.RefAlreadyExistsException

buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.3'
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.16.12"
    }
}

plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.6.14' apply false
    id "com.github.spotbugs" version "5.0.14" apply false
    id "at.zierler.yamlvalidator" version "1.5.0"
    id 'org.ajoberstar.grgit' version '5.2.0'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_17
    targetBytecodeVersion = JavaVersion.VERSION_17
}

yamlValidator {
    searchPaths = ['.circleci/']
    allowDuplicates = false
    searchRecursive = true
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.kafka"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}
